plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

repositories {
    jcenter()
}

dependencies {
	compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}

version = '1.0.2'

java {
 	sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    title "Lightstreamer Log Adapter $version API Reference"
    include '**/Logger.java'
    include '**/LoggerProvider.java'
    options.overview 'src/main/javadoc/overview.html'
    options.links 'https://lightstreamer.com/api/ls-javase-client/latest'
    options.addBooleanOption ('Xdoclint:none', true)
}

publishing {
    publications {
        maven(MavenPublication) {
        	groupId = 'com.lightstreamer'
            artifactId = 'ls-log-adapter-java'
            
            from components.java

            pom {
                name = 'Lightstreamer Log Adapter'
                description = 'Lightstreamer Log Adapter'
                url = 'http://www.lightstreamer.com'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                
                developers {
                    developer {
                        name = 'support'
                        email = 'support@lightstreamer.com'
                        organization = 'Lightstreamer'
                        organizationUrl = 'https://www.lightstreamer.com'
                    }
                }

                scm {
                    connection = "scm:git:https://github.com/Lightstreamer/Log-adapter-java.git"
                    developerConnection = "scm:git:https://github.com/Lightstreamer/Log-adapter-java.git"
                    url = 'https://github.com/Lightstreamer/Log-adapter-java'
                }
            }
        }
    }

    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ? 'https://oss.sonatype.org/content/repositories/snapshots' : 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}
